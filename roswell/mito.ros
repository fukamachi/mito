#|-*- mode:lisp -*-|#
#|
exec ros -Q -- $0 "$@"
|#
(ql:quickload '(:mito-migration :dbi :sxql :uiop :alexandria) :silent t)

(defpackage :ros.script.roswell/mito.3663426439
  (:use #:cl
        #:mito.migration)
  (:import-from #:mito.core
                #:*connection*
                #:*mito-logger-stream*
                #:execute-sql)
  (:import-from #:dbi
                #:with-transaction
                #:connect
                #:<dbi-database-error>)
  (:import-from #:sxql
                #:yield)
  (:import-from #:alexandria
                #:nconcf))
(in-package :ros.script.roswell/mito.3663426439)

(defparameter *default-migration-directory*
  (merge-pathnames #P"db/" *default-pathname-defaults*))

(defmacro case-equal (keyform &body cases)
  (let ((g-keyform (gensym "KEYFORM")))
    `(let ((,g-keyform ,keyform))
       (cond
         ,@(loop for (case . body) in cases
                 if (eq case 'otherwise)
                   collect `(t ,@body)
                 else
                   collect `((find ,g-keyform ',(if (listp case)
                                                    case
                                                    (list case))
                                   :test #'equal)
                             ,@body))))))

(defun parse-args (args)
  (let ((connect-args '())
        driver-type
        (directory *default-migration-directory*)
        (systems '())
        dry-run)
    (loop for option = (pop args)
          while option
          do (case-equal option
               (("-t" "--type")
                (let ((driver (pop args)))
                  (setf driver-type
                        (if driver
                            (intern (string-upcase driver) :keyword)
                            nil))))
               (("-d" "--database")
                (nconcf connect-args (list :database-name (pop args))))
               (("-u" "--username")
                (nconcf connect-args (list :username (pop args))))
               (("-p" "--password")
                (nconcf connect-args (list :password (pop args))))
               (("-s" "--system")
                (push (pop args) systems))
               (("-D" "--directory")
                (setf directory (uiop:ensure-directory-pathname (pop args))))
               ("--dry-run"
                (setf dry-run t))
               (otherwise (error "Unknown option: ~A" option))))
    (values (cons driver-type connect-args) directory (nreverse systems) dry-run)))

(defun print-usage ()
  (format *error-output* "~&Usage: mito command [option...]

Commands:
    generate-migrations
    migrate

Options:
    -t, --type DRIVER-TYPE          DBI driver type (one of \"mysql\", \"postgres\" or \"sqlite3\")
    -d, --database DATABASE-NAME    Database name to use
    -u, --username USERNAME         Username for RDBMS
    -p, --password PASSWORD         Password for RDBMS
    -s, --system SYSTEM             ASDF system to load (several -s's allowed)
    -D, --directory DIRECTORY       Directory path to keep migration SQL files (default: \"~A\")
    --dry-run                       List SQL expressions to migrate
"
          *default-migration-directory*))

(defun main (&optional command &rest argv)
  (unless (find command '("generate-migrations" "migrate") :test #'string=)
    (when command
      (format *error-output* "~&Invalid command: ~A~%" command))
    (print-usage)
    (uiop:quit -1))

  (multiple-value-bind (connect-args directory systems dry-run)
      (handler-case (parse-args argv)
        (error (e) (format *error-output* "~&~A~%" e) (uiop:quit -1)))
    (when (null (first connect-args))
      (format *error-output* "~&--type is required.~%")
      (uiop:quit -1))

    (dolist (system systems)
      (format t "~&Loading ~S...~%" system)
      (ql:quickload system :silent t))
    (write-char #\Newline)
    (handler-case
        (let ((*connection* (handler-case (let ((*standard-output* (make-broadcast-stream)))
                                            (apply #'dbi:connect connect-args))
                              (ql:system-not-found ()
                                (format *error-output* "~&Unknown driver type: ~A~%" (first connect-args))
                                (uiop:quit -1))
                              (error (e)
                                (format *error-output* "~&Failed to connect to the database:~%  ~A~%" e)
                                (uiop:quit -1)))))
          (setf *mito-logger-stream* nil)
          (case-equal command
            ("generate-migrations"
             (mito.migration:generate-migrations directory))
            ("migrate"
             (mito.migration:migrate directory :dry-run dry-run))))
      (dbi:<dbi-database-error> (e)
        (format *error-output* "~&~A~%" e)
        (uiop:quit -1)))))
